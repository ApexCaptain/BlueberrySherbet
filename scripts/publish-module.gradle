apply plugin: 'maven-publish'
apply plugin: 'signing'

task androidSourcesJar(type: Jar) {
    archiveClassifier.set("sources")
    if(project.plugins.findPlugin("com.android.library")) {
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}

artifacts {
    archives androidSourcesJar
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION
                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                artifact androidSourcesJar
                // artifact javadocJar

                // Mostly self-explanatory metadata
                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = 'Bluetooth Low Energy Handling SDK'
                    url = 'https://github.com/ApexCaptain/BlueberrySherbet'
                    licenses {
                        license {
                            name = 'Blueberry Sherbet License'
                            url = 'https://github.com/ApexCaptain/BlueberrySherbet/blob/master/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            id = 'ApexCaptain'
                            name = 'SangHun Lee'
                            email = 'aytenee93@gmail.com'
                        }
                    }
                    // Version control info - if you're using GitHub, follow the
                    // format as seen here
                    scm {
                        connection = 'scm:git:github.com/ApexCaptain/BlueberrySherbet.git'
                        developerConnection = 'scm:git:ssh://github.com/ApexCaptain/BlueberrySherbet.git'
                        url = 'https://github.com/ApexCaptain/BlueberrySherbet/tree/master'
                    }
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
            rootProject.ext["signing.keyId"],
            rootProject.ext["signing.key"],
            rootProject.ext["signing.password"],
    )
    sign publishing.publications
}
